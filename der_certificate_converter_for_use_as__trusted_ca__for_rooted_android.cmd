@echo off
::--------------------------------------------------------------------------------------------------------------------------
:: drag&drop a DER-certificate, to generate PEM and then '.0' file with its name the PEM-subject-MD5-hash.
:: note: you may delete any '.pem' (temporary) and '.old' (backup) files generated by this this batch-file.

:: move the '.0' file to your rooted device under /system/etc/security/cacerts/  
:: set permission to 644 and ownership to root (0), reboot and the certificate will be installed as Trusted CA.
:: Note that some apps will use certificate-pinning (try installing Xposed-framework with the module SSLUnpinning).
::--------------------------------------------------------------------------------------------------------------------------
:: This project uses openssl and few exe of gnuwin32 (see README.md files for more information).
::--------------------------------------------------------------------------------------------------------------------------
:: https://github.com/eladkarako/der_certificate_converter_for_use_as__trusted_ca__for_rooted_android
:: https://github.com/eladkarako/der_certificate_converter_for_use_as__trusted_ca__for_rooted_android/archive/master.zip
::--------------------------------------------------------------------------------------------------------------------------
::                                                        Elad Karako. October 2020.
::--------------------------------------------------------------------------------------------------------------------------




set "EXIT_CODE=0"

::-------------------------------------------- check if argument provided, it exists, it is a file.
if ["%~1"] EQU [""] ( goto ERROR_NO_ARG )
if not exist "%~s1" ( goto ERROR_NO_ARG )
if exist %~s1\NUL   ( goto ERROR_NO_ARG )

set "FILE_OPENSSL=%~sdp0openssl\openssl.exe"
set "FILE_HEAD=%~sdp0coreutils\head.exe"
set "FILE_INPUT_DER=%~sf1"
set "FILE_OUTPUT_PEM=%~sdp1%~n1.pem"
set "SUBJECT_MD5_HASH="
set "FILE_OUTPUT_ZERO="

pushd "%~sdp1"

del /f /q "%~n1.pem.old"           1>nul 2>nul
ren "%~n1.pem" "%~n1.pem.old"      1>nul 2>nul

call "%FILE_OPENSSL%" x509 -inform DER -in %FILE_INPUT_DER% -out "%FILE_OUTPUT_PEM%"
set "EXIT_CODE=%ErrorLevel%"
if ["%EXIT_CODE%"] NEQ ["0"]     ( goto ERROR_OPENSSL_CONVERTION_DER_TO_PEM )
if not exist "%FILE_OUTPUT_PEM%" ( goto ERROR_OPENSSL_CONVERTION_DER_TO_PEM )

echo [INFO] '%~nx1' was successfully converted to '%~n1.pem'  1>&2
echo. 1>&2

for /f "tokens=*" %%a in ('call "%FILE_OPENSSL%" x509 -inform PEM -in "%FILE_OUTPUT_PEM%" -noout -subject_hash_old ^| "%FILE_HEAD%" -1 ') do (set "SUBJECT_MD5_HASH=%%a" )
set "EXIT_CODE=%ErrorLevel%"
if ["%EXIT_CODE%"] NEQ ["0"] ( goto ERROR_OPENSSL_FETCH_SUBJECT_MD5_HASH_FROM_PEM )

echo [INFO] successfully fetched the subject-MD5-hash [%SUBJECT_MD5_HASH%] from '%~n1.pem'.  1>&2
echo. 1>&2

set "FILE_OUTPUT_ZERO=%~sdp1%SUBJECT_MD5_HASH%.0"
del /f /q "%SUBJECT_MD5_HASH%.0.old"                    1>nul 2>nul
ren "%SUBJECT_MD5_HASH%.0" "%SUBJECT_MD5_HASH%.0.old"   1>nul 2>nul

copy /b /y /v "%FILE_OUTPUT_PEM%" "%FILE_OUTPUT_ZERO%"  1>nul 2>nul
set "EXIT_CODE=%ErrorLevel%"
if ["%EXIT_CODE%"] NEQ ["0"]      ( goto ERROR_COPY_PEM_TO_MD5HASHDOTZERO )
if not exist "%FILE_OUTPUT_ZERO%" ( goto ERROR_COPY_PEM_TO_MD5HASHDOTZERO )
  
echo [INFO] successfully generated '%SUBJECT_MD5_HASH%.0' from '%~n1.pem' (note: you do not need the PEM file). 1>&2
echo. 1>&2

echo [INFO] now that the convertion is done,  1>&2
echo   you should copy '%SUBJECT_MD5_HASH%.0' to  1>&2
echo   your rooted Android device under '/system/etc/security/cacerts/'  1>&2
echo   make sure to set its permission to 644 and ownership and group to root (0).       1>&2
echo   reboot your device and you will have it under Trusted CA.       1>&2
echo. 1>&2

goto END


::---------------------------------------------------------------------------------------------------


:ERROR_NO_ARG
  set "EXIT_CODE=111"
  echo [ERROR] please provide a single-argument, a DER file.   1>&2
  goto END

:ERROR_OPENSSL_CONVERTION_DER_TO_PEM
  echo [ERROR] openssl reported an error while converting               1>&2
  echo    (input) '%~f1'             1>&2
  echo    to                         1>&2
  echo    (output) '%~dp1%~n1.pem'   1>&2
  echo    make sure the input is a valid DER certificate (corrupted?).  1>&2
  goto END

:ERROR_OPENSSL_FETCH_SUBJECT_MD5_HASH_FROM_PEM
  echo [ERROR] openssl reported an error while trying to fetch the subject MD5-hash from 1>&2
  echo    (input) '%~dp1%~n1.pem'   1>&2
  echo    it should not happen, but maybe the PEM is not valid (corrupted?).   1>&2
  goto END
  
:END
  echo [INFO] Done. [EXIT_CODE: %EXIT_CODE%].          1>&2
  pause
  popd
  exit /b %EXIT_CODE%
  